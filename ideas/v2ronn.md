man pages: write in Markdown, maintain by Git, release in \*roff
====

what
----
This is a technique which permits:

* to write tha man pages in markdown
* an easy management of the man pages using git and cmake
* to avoid the dependence from the markdown translator to release the source tree or for the source packages of Linux distributions.

why
----
It is doubtless that the *roff syntax is obsolete and boring

how
----
We have integrated ronn in our developing process.

Man pages are in a specific directory named `man`.
The `CMakeLists.txt` include just one line regarding the man pages:
```
add_subdirectory(man)
```

The file `man/CMakeLists.txt` translates all the files with `.[1-8].ronn` suffix in man pages `.[1.8]`
storing the result in the source tree (not in the build tree) using a makefile. So if the man page
is newer than the ronn source the translaiton phase is skipped. 
So ronn is needed only to build the man pages if the ronn source has been updated.
In this way git can track the versions both of the ronn source and of the \*roff man pages.

Neither `man/CMakeLists.txt` nor `man/Makefile` have project specific definitions, so both can be copied
in any new project.

details
----

This is `man/CMakeLists.txt`:

```
cmake_minimum_required(VERSION 3.7)
  
set(RONN_ORGANIZATION "VirtualSquare")
set(RONN_ARGS --organization=${RONN_ORGANIZATION})

# ### ronn pages

file(GLOB VU_RONN_PAGES ${CMAKE_CURRENT_SOURCE_DIR}/*.[1-8].ronn)
set(VU_MAN_FILES)
foreach(VU_RONN_PATH IN LISTS VU_RONN_PAGES)
# VU_RONNPAGE: basename of VU_RONN_PATH
  get_filename_component(VU_RONNPAGE ${VU_RONN_PATH} NAME)
# VU_MANPAGE: VU_RONNPAGE without the suffix
  string(REGEX REPLACE "\.ronn$" "" VU_MANPAGE ${VU_RONNPAGE})
  list(APPEND VU_MAN_FILES ${VU_MANPAGE})
endforeach(VU_RONN_PATH)

add_custom_target(${PROJECT_NAME}_manpages ALL make RONN_ARGS="${RONN_ARGS}"  ${VU_MAN_FILES}
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR})

### man pages
file(GLOB VU_MAN_PAGES ${CMAKE_CURRENT_SOURCE_DIR}/*.[1-8])
foreach(VU_MAN_PATH IN LISTS VU_MAN_PAGES)
  get_filename_component(VU_MANPAGE ${VU_MAN_PATH} NAME)
  string(REGEX REPLACE ".*\\." "" MAN_CHAPTER ${VU_MANPAGE})
  install(FILES ${VU_MAN_PATH} DESTINATION ${CMAKE_INSTALL_MANDIR}/man${MAN_CHAPTER})
endforeach(VU_MAN_PATH)
```
The code after the comment `### ronn pages` creates a list (`VU_MAN_FILES`) including all the pages
tha can be generated by ronn (i.e. a list of all the `.ronn` files without the suffix).

The target `${PROJECT_NAME}_manpages`  runs make o create/update these man pages (if needed).

The code after the comment `### man pages` installs the man pages (generated by ronn or simply written in \*roff)
in the right `chapter`.

and this is `man/Makefile`:

```make
RONN=ronn
RONNOK := $(shell command -v ${RONN} 2> /dev/null)

none:

% : %.ronn
ifdef RONNOK
# copy copyright notice
    grep "^\.\\\\\"" $< > $@ || true
# run ronn
    $(RONN) -r ${RONN_ARGS} --pipe $< >> $@
# delete useless trailing "" in .TH
    sed -i '/^\.TH /s/ ""$$//' $@
else
    echo "${RONN} is not available. Manpage $@ cannot be updated" >/dev/stderr >&2
endif
```

This Makefile calls ronn to create the man pages. There is a workaround to copy the copyright notice from the ronn source.

where
----
Many projects: libpam-net, vdeplug_slirp, libvolatilestream, libvpoll-eventfd, libfduserdata, vdeplug_pcap, vdeplug_vdesl, nlinline, randmac, libvdeslirp, libstropt, vuos, vdeplug_vlan, libvdestack, ...

references:
----
[ronn-ng](https://github.com/apjanke/ronn-ng)
